# Justfile

# Initialization
set shell := ["zsh", "-l", "-cu"]
### set shell := ["zsh", "-l", "-c"]
### set shell := ["bash", "-c"]

# set script-interpreter := ['bash', '-eu']

# Variables
JUST_IMAGE_NAME     := "fastapi-uv"
JUST_CONTAINER_NAME := "fastapi-uv-container"
JUST_REGISTRY:= "registry.gitlab.com"
JUST_REG_USERNAME:= "acola"
JUST_REG_PASSWORD:= "env:GITLAB_TOKEN"
JUST_REG_PATH:= "acola/fastapi-uv"
# JUST_CONTAINER_TAG:= "$(git rev-parse --short=8 HEAD)"
JUST_CONTAINER_TAG:= "$(git describe --tags --abbrev=4)"
JUST_CONTAINER_SRC:= "."

# JUST_DOCKERFILE     := "Dockerfile"         # Python 3.12 image
JUST_DOCKERFILE     := "alpine.dockerfile"    # Python 3.12 Alpine image
# JUST_DOCKERFILE     := "ubuntu.dockerfile"  # Ubuntu:latest image

JUST_BUILD_OPTS     := ""
# JUST_BUILD_OPTS     := "--no-cache"

JUST_IMAGE_TAG      := "latest"
# JUST_IMAGE_TAG      := "standard"
# JUST_IMAGE_TAG      := "alpine"
# JUST_IMAGE_TAG      := "ubuntu"

JUST_PORT           := "8001"
JUST_PORT_DEV       := "8008"
JUST_PORT_DOC       := "8009"

# List ğŸ“œ all recipes (this!)
help:
    @just --list

# Install ğŸ§± the virtual environment and install the pre-commit hooks
code-install:
    @echo "ğŸš€ Creating virtual environment using uv"
    uv sync
    uv run pre-commit install

# Run ğŸ› ï¸ the app in development mode with reload â™»ï¸  (alias: dev)
code-run-dev:
    @/bin/echo -e "\nğŸš€ Running app in development mode (with reload)\n"
    #@ uv run --with "fastapi[standard]" fastapi dev ./src/fastapi_uv/main.py --port {{JUST_PORT_DEV}}
    uv run fastapi dev ./src/fastapi_uv/main.py --port {{JUST_PORT_DEV}}
    @# @uv run uvicorn src.fastapi_uv.main:app --reload --port {{JUST_PORT_DEV}}

alias dev := code-run-dev

# Test ğŸ§ª code and generate test Coverage report  (alias: test)
code-test:
    @echo -e "\nğŸš€ Testing code: Running pytest with coverage\n"
    uv run python -m pytest --cov --cov-config=pyproject.toml --cov-report=xml

alias test := code-test

# Upgrade ğŸˆ Python packages
code-upgrade-packages:
    @echo "ğŸš€ Upgrading Python packages with UV"
    uv sync --upgrade

# Run ğŸ” pre-commit checks
code-pre-commit-check:
    @echo "ğŸš€ Running pre-commit checks"
    uv run pre-commit run --all-files

# Run ğŸ” code quality tools  (alias: check)
code-check:
    @echo -e "\nğŸš€ Checking lock file consistency with 'pyproject.toml'"
    uv lock --locked
    @echo -e "\nğŸš€ Linting code: Running pre-commit"
    uv run pre-commit run --all-files
    @echo -e "\nğŸš€ Static type checking: Running mypy"
    uv run mypy ./src
    @echo -e "\nğŸš€ Checking for obsolete dependencies: Running deptry"
    uv run deptry ./src
    @echo -e "\nğŸš€ Running Pyright for type checking"
    uv run --with pyright pyright ./src

alias check := code-check

# Scan ğŸ•µğŸ»â€â™‚ï¸ the code for security issues using Bandit
code-scan-bandit:
    @echo -e "ğŸ•µğŸ»â€â™‚ï¸ Scanning code for security issues using Bandit\n"
    -uvx bandit -r ./src/

# Scan ğŸ•µğŸ»â€â™‚ï¸ the code for security issues using Semgrep
code-scan-semgrep:
    @echo -e "ğŸ•µğŸ»â€â™‚ï¸ Scanning code for security issues using Semgrep\n"
    uvx --with semgrep semgrep --config=auto src/

# # Run SonarQube scan
# scan-code-sonarqube:
#     @echo "ğŸš€ Running SonarQube scan"
#     @docker run --rm -d --name sonarqube -e SONAR_ES_BOOTSTRAP_CHECKS_DISABLE=true -p 9000:9000 sonarqube:latest
#     @sleep 60
#     @sonar-scanner
#     @docker stop sonarqube

# Build ğŸ“¦ wheel file
code-package-build:
    @echo -e "ğŸš€ Creating wheel file\n"
    uvx --from build pyproject-build --installer uv

# Clean ğŸ§¹ build artifacts
code-package-clean:
    @echo -e "ğŸš€ Removing build artifacts"
    uv run python -c "import shutil; import os; shutil.rmtree('dist') if os.path.exists('dist') else None"

# Publish ğŸ“° a release to PyPI (requires API token)
code-package-publish:
    @echo -e "ğŸš€ Publishing\n"
    hatch publish

# Build ğŸ“¦ and publish ğŸ“°
code-package-build-publish:  code-package-build  code-package-publish

# ğŸ·ï¸ Update app version in pyproject.toml
code-bump-version:
    @echo -e "ğŸš€ Updating app version in pyproject.toml\n"
    uv run -q --with tomli_w ./scripts/update_version.py
    uv lock
    git add pyproject.toml uv.lock
    git commit -m "Bump version"

alias bump := code-bump-version

# ğŸ¯ Test the app connecting to the the API with a curl GET
code-app-test:
    @echo -e "\nğŸš€ Testing the app connecting to the the API\n"
    curl -s http://localhost:{{JUST_PORT}} | jq

alias app-test := code-app-test

## Container recipes
# Build ğŸ“¦ the container  (alias: build)
container-build *build-options:
    @echo -e "\nğŸš€ Building container\n"
    docker build {{JUST_BUILD_OPTS}} {{build-options}} . -f {{JUST_DOCKERFILE}} -t {{JUST_IMAGE_NAME}}:{{JUST_IMAGE_TAG}} --load

alias build := container-build

# Start ğŸš€ the container  (alias: start)
container-start: container-build
    @echo -e "\nğŸš€ Starting the container {{JUST_CONTAINER_NAME}}"
    docker run --rm --name {{JUST_CONTAINER_NAME}} --detach -p {{JUST_PORT}}:8001 {{JUST_IMAGE_NAME}}:{{JUST_IMAGE_TAG}}
    @echo -e "\nğŸ Container available: ğŸ”— http://localhost:{{JUST_PORT}}"

alias start := container-start

# Start ğŸš€ the container from the image (i.e: ghcr.io/user/app:latest)
container-start-from-image $ARG_IMAGE_NAME:
    #!/usr/bin/env bash
    echo -e "\nğŸš€ Starting {{JUST_CONTAINER_NAME}} from image:\nâ¡ï¸ {{ARG_IMAGE_NAME}}\n"
    docker run --rm --name {{JUST_CONTAINER_NAME}} --detach -p {{JUST_PORT}}:8001 {{ARG_IMAGE_NAME}}
    if [[ $? -eq 125 ]]; then
        echo -e "\nğŸš¨ Error: container {{JUST_CONTAINER_NAME}} already exists. Fix:\n  just stop"
    else
        echo -e "\nğŸ Container available: ğŸ”— http://localhost:{{JUST_PORT}}"
    fi

alias start-image := container-start-from-image

# Push ğŸ“¦ the container to Docker registry
container-push:
    @echo "ğŸ“¦ Pushing container to Docker registry"
    docker push {{JUST_IMAGE_NAME}}:{{JUST_IMAGE_TAG}}

# Stop ğŸ›‘ the running container  (alias: stop)
container-stop:
    @echo -e "\nğŸ›‘ Stopping container"
    -docker stop {{JUST_CONTAINER_NAME}}
    @echo -e "\nğŸ—‘ï¸ Removing container"
    -docker rm {{JUST_CONTAINER_NAME}}

alias stop := container-stop

# Shell into ğŸšª the container
container-shell:
    @echo -e "\nğŸšª Connecting to container shell"
    docker exec -it  {{JUST_CONTAINER_NAME}} /bin/bash

# Remove ğŸ—‘ï¸ the container
container-remove:
    @echo -e "ğŸ—‘ï¸ Removing container\n"
    docker rm {{JUST_CONTAINER_NAME}}

# Remove ğŸ—‘ï¸ the Docker image
container-image-remove:
    @echo -e "ğŸ—‘ï¸ Removing container image\n"
    docker rmi {{JUST_IMAGE_NAME}}:{{JUST_IMAGE_TAG}}
    @echo -e "\nğŸ—‘ï¸ Container image removed"

# View ğŸ“œ logs of the running container
container-logs:
    @echo "ğŸ“œ View logs of the running container"
    docker logs {{JUST_CONTAINER_NAME}}

# View ğŸ“œ and follow ğŸ¿ logs of the running container
container-logs-f:
    @echo "ğŸ“œ View and follow logs of the running container"
    docker logs -f {{JUST_CONTAINER_NAME}}

# Scan ğŸ•µğŸ»â€â™‚ï¸  the container for vulnerabilities using Trivy ğŸ¯
container-scan-trivy: container-build
    @echo -e "\nğŸ•µğŸ»â€â™‚ï¸  Scanning container for vulnerabilities using Trivy ğŸ¯\n"
    # @trivy image {{JUST_IMAGE_NAME}}
    # @trivy image --severity HIGH,CRITICAL {{JUST_IMAGE_NAME}}
    trivy image --severity MEDIUM,HIGH,CRITICAL --ignore-unfixed {{JUST_IMAGE_NAME}}:{{JUST_IMAGE_TAG}}

# Scan ğŸ•µğŸ»â€â™‚ï¸  the container for vulnerabilities using Grype ğŸ‘¾
container-scan-grype: container-build
    @echo -e "\nğŸ•µğŸ»â€â™‚ï¸  Scanning container for vulnerabilities using Grype ğŸ‘¾\n"
    grype --only-fixed {{JUST_IMAGE_NAME}}:{{JUST_IMAGE_TAG}}


## Documentation recipes
# Test ğŸ“š if documentation can be built without warnings or errors
docs-test:
    uv run mkdocs build -s

# Build ğŸ“š and serve the documentation
docs:
    @echo "ğŸ“š Serving documentation on ğŸ”— http://127.0.0.1:{{JUST_PORT_DOC}}"
    uv run mkdocs serve -a 127.0.0.1:{{JUST_PORT_DOC}}

# ğŸ—¡ï¸ Build the container with Dagger
dagger-build:
    @echo "\nğŸ—¡ï¸ Dagger build\n"
    dagger call build --src {{JUST_CONTAINER_SRC}}

# ğŸ—¡ï¸ Build and push the container to the registry with Dagger
dagger-build-push:
    @echo "\nğŸ—¡ï¸ Dagger build and push\n"
    dagger call build-push --registry={{JUST_REGISTRY}} --username={{JUST_REG_USERNAME}} --password={{JUST_REG_PASSWORD}} --path {{JUST_REG_PATH}} --image {{JUST_IMAGE_NAME}} --tag {{JUST_CONTAINER_TAG}} --src {{JUST_CONTAINER_SRC}}

# ğŸ—¡ï¸ Test the container with Dagger ğŸ§ª
dagger-test:
    @echo "\nğŸ—¡ï¸ Dagger test ğŸ§ª\n"
    dagger call test --src {{JUST_CONTAINER_SRC}}
