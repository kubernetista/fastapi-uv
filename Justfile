# Justfile

# Initialization
set shell := ["zsh", "-l", "-cu"]
### set shell := ["zsh", "-l", "-c"]
### set shell := ["bash", "-c"]

# set script-interpreter := ['bash', '-eu']

# Variables
VAR_IMAGE_NAME     := "fastapi-uv"
VAR_CONTAINER_NAME := "fastapi-uv-container"

# VAR_DOCKERFILE     := "Dockerfile"
VAR_DOCKERFILE     := "alpine.dockerfile"
# VAR_DOCKERFILE     := "ubuntu.dockerfile"

VAR_BUILD_OPTS     := ""
# VAR_BUILD_OPTS     := "--no-cache"

# VAR_IMAGE_TAG      := "latest"
# VAR_IMAGE_TAG      := "standard"
VAR_IMAGE_TAG      := "alpine"
# VAR_IMAGE_TAG      := "ubuntu"

VAR_PORT           := "8001"
VAR_PORT_DEV       := "8008"
VAR_PORT_DOC       := "8009"

# List ğŸ“œ all recipes (this!)
help:
    @just --list

# Install ğŸ§± the virtual environment and install the pre-commit hooks
code-install:
    @echo "ğŸš€ Creating virtual environment using uv"
    uv sync
    uv run pre-commit install

# Run ğŸ› ï¸ the app in development mode with reload â™»ï¸  (alias: dev)
code-run:
    @/bin/echo -e "\nğŸš€ Running app in development mode (with reload)\n"
    #@ uv run --with "fastapi[standard]" fastapi dev ./src/fastapi_uv/main.py --port {{VAR_PORT_DEV}}
    uv run fastapi dev ./src/fastapi_uv/main.py --port {{VAR_PORT_DEV}}
    @# @uv run uvicorn src.fastapi_uv.main:app --reload --port {{VAR_PORT_DEV}}

alias dev := code-run

# Test ğŸ§ª code and generate test Coverage report  (alias: test)
code-test:
    @echo -e "\nğŸš€ Testing code: Running pytest with coverage\n"
    uv run python -m pytest --cov --cov-config=pyproject.toml --cov-report=xml

alias test := code-test

# Upgrade ğŸˆ dependencies
code-upgrade-dependencies:
    @echo "ğŸš€ Upgrading dependencies"
    uv sync --upgrade

# Run ğŸ” pre-commit checks
code-pre-commit-check:
    @echo "ğŸš€ Running pre-commit checks"
    uv run pre-commit run --all-files

# Run ğŸ” code quality tools
code-check:
    @echo -e "\nğŸš€ Checking lock file consistency with 'pyproject.toml'"
    uv lock --locked
    @echo -e "\nğŸš€ Linting code: Running pre-commit"
    uv run pre-commit run --all-files
    @echo -e "\nğŸš€ Static type checking: Running mypy"
    uv run mypy ./src
    @echo -e "\nğŸš€ Checking for obsolete dependencies: Running deptry"
    uv run deptry ./src
    @echo -e "\nğŸš€ Running Pyright for type checking"
    uv run --with pyright pyright ./src

# Scan ğŸ•µğŸ»â€â™‚ï¸ the code for security issues using Bandit
code-scan-bandit:
    @echo -e "ğŸ•µğŸ»â€â™‚ï¸ Scanning code for security issues using Bandit\n"
    -uvx bandit -r ./src/

# Scan ğŸ•µğŸ»â€â™‚ï¸ the code for security issues using Semgrep
code-scan-semgrep:
    @echo -e "ğŸ•µğŸ»â€â™‚ï¸ Scanning code for security issues using Semgrep\n"
    uvx --with semgrep semgrep --config=auto src/

# # Run SonarQube scan
# scan-code-sonarqube:
#     @echo "ğŸš€ Running SonarQube scan"
#     @docker run --rm -d --name sonarqube -e SONAR_ES_BOOTSTRAP_CHECKS_DISABLE=true -p 9000:9000 sonarqube:latest
#     @sleep 60
#     @sonar-scanner
#     @docker stop sonarqube

# Build ğŸ“¦ wheel file
code-package-build:
    @echo -e "ğŸš€ Creating wheel file\n"
    uvx --from build pyproject-build --installer uv

# Clean ğŸ§¹ build artifacts
code-package-clean:
    @echo -e "ğŸš€ Removing build artifacts"
    uv run python -c "import shutil; import os; shutil.rmtree('dist') if os.path.exists('dist') else None"

# Publish ğŸ“° a release to PyPI (requires API token)
code-package-publish:
    @echo -e "ğŸš€ Publishing\n"
    hatch publish

# Build ğŸ“¦ and publish ğŸ“°
code-package-build-publish:  code-package-build  code-package-publish

## Container recipes
# Build ğŸ“¦ the container
container-build:
    @echo -e "\nğŸš€ Building container\n"
    docker build {{VAR_BUILD_OPTS}} . -f {{VAR_DOCKERFILE}} -t {{VAR_IMAGE_NAME}}:{{VAR_IMAGE_TAG}} --load

# Start ğŸš€ the container
container-start: container-build
    @echo -e "\nğŸš€ Starting the container {{VAR_CONTAINER_NAME}}"
    docker run --rm --name {{VAR_CONTAINER_NAME}} --detach -p {{VAR_PORT}}:8001 {{VAR_IMAGE_NAME}}:{{VAR_IMAGE_TAG}}
    @echo -e "\nğŸ Container available: ğŸ”— http://localhost:{{VAR_PORT}}"

# Push ğŸ“¦ the container to Docker registry
container-push:
    @echo "ğŸ“¦ Pushing container to Docker registry"
    docker push {{VAR_IMAGE_NAME}}:{{VAR_IMAGE_TAG}}

# Stop ğŸ›‘ the running container
container-stop:
    @echo -e "\nğŸ›‘ Stopping container"
    -docker stop {{VAR_CONTAINER_NAME}}
    @echo -e "\nğŸ—‘ï¸ Removing container"
    -docker rm {{VAR_CONTAINER_NAME}}

# Shell into ğŸšª the container
container-shell:
    @echo -e "\nğŸšª Connecting to container shell"
    docker exec -it  {{VAR_CONTAINER_NAME}} /bin/bash

# Remove ğŸ—‘ï¸ the container
container-remove:
    @echo -e "ğŸ—‘ï¸ Removing container\n"
    docker rm {{VAR_CONTAINER_NAME}}

# Remove ğŸ—‘ï¸ the Docker image
container-image-remove:
    @echo -e "ğŸ—‘ï¸ Removing container image\n"
    docker rmi {{VAR_IMAGE_NAME}}:{{VAR_IMAGE_TAG}}
    @echo -e "\nğŸ—‘ï¸ Container image removed"

# View ğŸ“œ logs of the running container
container-logs:
    @echo "ğŸ“œ View logs of the running container"
    docker logs {{VAR_CONTAINER_NAME}}

# View ğŸ“œ and follow ğŸ¿ logs of the running container
container-logs-f:
    @echo "ğŸ“œ View and follow logs of the running container"
    docker logs -f {{VAR_CONTAINER_NAME}}

# Scan ğŸ•µğŸ»â€â™‚ï¸  the container for vulnerabilities using Trivy ğŸ¯
container-scan-trivy: container-build
    @echo -e "\nğŸ•µğŸ»â€â™‚ï¸  Scanning container for vulnerabilities using Trivy ğŸ¯\n"
    # @trivy image {{VAR_IMAGE_NAME}}
    # @trivy image --severity HIGH,CRITICAL {{VAR_IMAGE_NAME}}
    trivy image --severity MEDIUM,HIGH,CRITICAL --ignore-unfixed {{VAR_IMAGE_NAME}}:{{VAR_IMAGE_TAG}}

# Scan ğŸ•µğŸ»â€â™‚ï¸  the container for vulnerabilities using Grype ğŸ‘¾
container-scan-grype: container-build
    @echo -e "\nğŸ•µğŸ»â€â™‚ï¸  Scanning container for vulnerabilities using Grype ğŸ‘¾\n"
    grype --only-fixed {{VAR_IMAGE_NAME}}:{{VAR_IMAGE_TAG}}


## Documentation recipes
# Test ğŸ“š if documentation can be built without warnings or errors
docs-test:
    uv run mkdocs build -s

# Build ğŸ“š and serve the documentation
docs:
    @echo "ğŸ“š Serving documentation on ğŸ”— http://127.0.0.1:{{VAR_PORT_DOC}}"
    uv run mkdocs serve -a 127.0.0.1:{{VAR_PORT_DOC}}
