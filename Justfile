# Justfile

# Variables
VAR_IMAGE_NAME := "fastapi-uv:latest"
VAR_CONTAINER_NAME := "fastapi-uv-container"
VAR_PORT := "8001"
VAR_PORT_DEV := "8008"
VAR_PORT_DOC := "8009"

# List ğŸ“œ all recipes (this!)
help:
    @just --list

# Install ğŸ§± the virtual environment and install the pre-commit hooks
install:
    @echo "ğŸš€ Creating virtual environment using uv"
    @uv sync
    @uv run pre-commit install

# Upgrade ğŸˆ dependencies
upgrade-dependencies:
    @echo "ğŸš€ Upgrading dependencies"
    @uv sync --upgrade

# Run ğŸ” pre-commit checks
pre-commit-check:
    @echo "ğŸš€ Running pre-commit checks"
    @uv run pre-commit run --all-files

# Run ğŸ” code quality tools
check:
    @echo -e "\nğŸš€ Checking lock file consistency with 'pyproject.toml'"
    @uv lock --locked
    @echo -e "\nğŸš€ Linting code: Running pre-commit"
    @uv run pre-commit run --all-files
    @echo -e "\nğŸš€ Static type checking: Running mypy"
    @uv run mypy ./src
    @echo -e "\nğŸš€ Checking for obsolete dependencies: Running deptry"
    @uv run deptry ./src
    @echo -e "\nğŸš€ Running Pyright for type checking"
    @uv run --with pyright pyright ./src

# Test ğŸ§ª code and generate test Coverage report
test:
    @echo -e "\nğŸš€ Testing code: Running pytest with coverage\n"
    @uv run python -m pytest --cov --cov-config=pyproject.toml --cov-report=xml

# Scan ğŸ•µğŸ»â€â™‚ï¸ the code for security issues using Bandit
scan-code-bandit:
    @echo -e "ğŸ•µğŸ»â€â™‚ï¸ Scanning code for security issues using Bandit\n"
    -@uvx bandit -r ./src/

# Scan ğŸ•µğŸ»â€â™‚ï¸ the code for security issues using Semgrep
scan-code-semgrep:
    @echo -e "ğŸ•µğŸ»â€â™‚ï¸ Scanning code for security issues using Semgrep\n"
    @uvx --with semgrep semgrep --config=auto src/

# # Run SonarQube scan
# scan-code-sonarqube:
#     @echo "ğŸš€ Running SonarQube scan"
#     @docker run --rm -d --name sonarqube -e SONAR_ES_BOOTSTRAP_CHECKS_DISABLE=true -p 9000:9000 sonarqube:latest
#     @sleep 60
#     @sonar-scanner
#     @docker stop sonarqube

# Build ğŸ“¦ wheel file
build-pacakge:
    @echo "ğŸš€ Creating wheel file"
    @uvx --from build pyproject-build --installer uv

# Clean ğŸ§¹ build artifacts
clean-build:
    @echo "ğŸš€ Removing build artifacts"
    @uv run python -c "import shutil; import os; shutil.rmtree('dist') if os.path.exists('dist') else None"

# Publish ğŸ“° a release to PyPI (requires API token)
publish-pacakge:
    @echo "ğŸš€ Publishing."
    @hatch publish

# Build ğŸ“¦ and publish ğŸ“°
build-publish-pacakge: build-pacakge publish-pacakge

# Test ğŸ“š if documentation can be built without warnings or errors
docs-test:
    @uv run mkdocs build -s

# Build ğŸ“š and serve the documentation
docs:
    @echo "ğŸ“š Serving documentation on ğŸ”— http://127.0.0.1:{{VAR_PORT_DOC}}"
    @uv run mkdocs serve -a 127.0.0.1:{{VAR_PORT_DOC}}

# Run ğŸ› ï¸ the app in development mode with reload â™»ï¸  (alias: dev)
code-run:
    @echo -e "\nğŸš€ Running app in development mode (with reload)\n"
    #@ uv run --with "fastapi[standard]" fastapi dev ./src/fastapi_uv/main.py --port {{VAR_PORT_DEV}}
    @ uv run fastapi dev ./src/fastapi_uv/main.py --port {{VAR_PORT_DEV}}
    @# @uv run uvicorn src.fastapi_uv.main:app --reload --port {{VAR_PORT_DEV}}

alias dev := code-run

# Build ğŸ“¦ the container
container-build:
    @echo -e "\nğŸš€ Building container\n"
    docker build . -t {{VAR_IMAGE_NAME}} --load

# Scan ğŸ•µğŸ»â€â™‚ï¸  the container for vulnerabilities using Trivy ğŸ¯
scan-container-trivy: container-build
    @echo -e "\nğŸ•µğŸ»â€â™‚ï¸  Scanning container for vulnerabilities using Trivy ğŸ¯\n"
    # @trivy image {{VAR_IMAGE_NAME}}
    # @trivy image --severity HIGH,CRITICAL {{VAR_IMAGE_NAME}}
    @trivy image --severity MEDIUM,HIGH,CRITICAL --ignore-unfixed {{VAR_IMAGE_NAME}}

# Scan ğŸ•µğŸ»â€â™‚ï¸  the container for vulnerabilities using Grype ğŸ‘¾
scan-container-grype: container-build
    @echo -e "\nğŸ•µğŸ»â€â™‚ï¸  Scanning container for vulnerabilities using Grype ğŸ‘¾\n"
    @grype --only-fixed {{VAR_IMAGE_NAME}}

# Push ğŸ“¦ the container to Docker registry
container-push:
    @echo "ğŸ“¦ Pushing container to Docker registry"
    @docker push {{VAR_IMAGE_NAME}}

# Start ğŸš€ the container
container-start: container-build
    @echo -e "\nğŸš€ Starting the container {{VAR_CONTAINER_NAME}}\n"
    docker run --rm --name {{VAR_CONTAINER_NAME}} --detach -p {{VAR_PORT}}:8001 {{VAR_IMAGE_NAME}}
    @echo -e "\nContainer \"{{VAR_CONTAINER_NAME}}\" is accessible at http://localhost:{{VAR_PORT}}"

# Stop ğŸ›‘ the running container
container-stop:
    @echo -e "\nğŸ›‘ Stopping running container\n"
    @docker stop {{VAR_CONTAINER_NAME}}
    @echo -e "\nContainer \"{{VAR_CONTAINER_NAME}}\" removed"

# Remove ğŸ—‘ï¸ the container
container-remove:
    @echo "ğŸ—‘ï¸ Removing container"
    @docker rm {{VAR_CONTAINER_NAME}}

# Remove ğŸ—‘ï¸ the Docker image
image-remove:
    @echo "ğŸ—‘ï¸ Removing Docker image"
    @docker rmi {{VAR_IMAGE_NAME}}
    @echo "\nContainer image \"{{VAR_IMAGE_NAME}}\" removed"

# View ğŸ“œ logs of the running container
container-logs:
    @echo "ğŸ“œ View logs of the running container"
    @docker logs {{VAR_CONTAINER_NAME}}

# View ğŸ“œ and follow ğŸ¿ logs of the running container
container-logs-f:
    @echo "ğŸ“œ View and follow logs of the running container"
    @docker logs -f {{VAR_CONTAINER_NAME}}
